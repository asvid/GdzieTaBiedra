apply plugin: 'com.google.firebase.crashlytics'

android {
    defaultConfig {
        applicationId Build.appId
        buildToolsVersion Build.buildToolsVersion
        minSdkVersion Build.minSdkVersion
        targetSdkVersion Build.targetSdkVersion
        compileSdkVersion Build.compileSdkVersion
        versionCode 100
        versionName "3.0.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [appName: "@string/app_name"]
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            manifestPlaceholders = [appName: "Biedra - Debug"]
            debuggable true
            multiDexEnabled true
            minifyEnabled false
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }

            ext.alwaysUpdateBuildId = false
        }
    }
    flavorDimensions "stage"
    productFlavors {
        dev {
            dimension "stage"
            versionNameSuffix "-dev" // e.g 1.0-dev
            //      applicationIdSuffix '.dev'
            //      resConfigs("de", "xxhdpi")
        }
        prod {
            dimension "stage"
        }
    }
    variantFilter { variant ->
        // 'dev' flavor is only available for debug build
        if (variant.buildType.name != 'debug' && variant.getFlavors().get(0).name == "dev") {
            variant.setIgnore(true)
        }
        // 'prod' flavor is only available for release build
        if (!(variant.buildType.name == 'release') && variant.getFlavors().get(0).name == "prod") {
            variant.setIgnore(true)
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(path: ':domain')

    implementation Libs.kotlin
    implementation Libs.coroutines

    implementation Koin.android
    implementation Koin.scope
    implementation Koin.viewmodel
    implementation Koin.fragment

    implementation Android.cardView
    implementation Android.constraintLayout
    implementation Android.recyclerView
    implementation Android.appCompat
    implementation Android.material
    implementation Android.navigationFragment
    implementation Android.navigationFragmentKtx
    implementation Android.navigationUi
    implementation Android.navigationUiKtx
    implementation Android.livedataKtx
    implementation Android.viewmodelKtx

    implementation Libs.calendarView

    implementation Room.runtime
    kapt Room.compiler
    implementation Room.ktx

    implementation Google.maps
    implementation Google.location

    implementation Ktor.clientAndroid
    implementation Ktor.clientOkHttp
    implementation Ktor.ktorSerialization

    implementation Libs.jodaTime
    implementation Libs.dexter
    implementation Libs.timber

    implementation platform(Firebase.bom)
    implementation Firebase.crashlytics
    implementation Firebase.analytics

    debugImplementation Libs.leakcanaryDebug
}

dependencies {
    testImplementation TestDeps.junit
    androidTestImplementation TestDeps.runner
    androidTestImplementation TestDeps.espresso
    testImplementation Koin.test
    testImplementation Room.testing
}

apply plugin: 'com.google.gms.google-services'
apply from: '../DokkaConfig.gradle'